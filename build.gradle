buildscript {
  ext.kotlin_version = '1.1.4-2'

  repositories {
    mavenCentral()
  }
  dependencies {
    classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
  }
}

plugins {
  id "com.jfrog.bintray" version "1.7.3"
}

apply plugin: 'kotlin2js'
apply plugin: 'maven'
apply plugin: 'maven-publish'

group 'ch.grisu118.js'
version '0.2.1'

repositories {
  mavenCentral()
}

dependencies {
  compile "org.jetbrains.kotlin:kotlin-stdlib-js:$kotlin_version"
}

compileKotlin2Js {
  kotlinOptions.metaInfo = true
  kotlinOptions.outputFile = "${projectDir}/build/classes/main/jslogger.js"
  kotlinOptions.sourceMap = true
  kotlinOptions.moduleKind = 'umd'
}

jar.dependsOn compileKotlin2Js

// custom tasks for creating source/javadoc jars
task("sourcesJar", type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

// add javadoc/source jar tasks as artifacts
artifacts {
    archives sourcesJar
}

repositories {
    jcenter()
}

bintray {
  user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : ""
  key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey'): ""
  publications = ['Bintray']
  pkg {
    repo = 'kotlin'
    name = 'jslogger'
    licenses = ['MIT']
    vcsUrl = 'https://github.com/Grisu118/jslogger.git'
    version {
      name = project.version
      desc = 'First Release'
      released = new Date()
      vcsTag = project.version
    }
  }
}

publishing {
    publications {
        Bintray(MavenPublication) {
            from components.java
            groupId project.group
            artifactId 'jslogger'
            version project.version
            artifact sourcesJar
        }
    }
}
